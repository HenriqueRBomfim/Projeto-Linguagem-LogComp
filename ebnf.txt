program         ::= "Campanha" NEWLINE { statement } "FimCampanha" ;

statement       ::= entity_decl
                  | var_decl
                  | assign_stmt
                  | narrate_stmt
                  | if_stmt
                  | while_stmt
                  | block
                  | NEWLINE ;

entity_decl     ::= "criar" entity_type identifier "{" NEWLINE { attribute_assign } "}" ;

entity_type     ::= "personagem" | "monstro" | "item" ;

attribute_assign ::= identifier "=" expression ;

var_decl        ::= "definir" identifier type_decl [ "=" expression ] ;

type_decl       ::= "int" | "string" | "bool" ;

assign_stmt     ::= identifier "=" expression ;

narrate_stmt    ::= "narre" "(" expression ")" ;

if_stmt         ::= "se" expression "{" NEWLINE block "}" [ "senao" "{" NEWLINE block "}" ] ;

while_stmt      ::= "enquanto" expression "{" NEWLINE block "}" ;

block           ::= "{" NEWLINE { statement } "}" ;

expression      ::= bexpression ;

bexpression     ::= bterm { "ou" bterm } ;

bterm           ::= rexp { "e" rexp } ;

rexp            ::= expr [ ("<" | ">" | "igual" | "!=") expr ] ;

expr            ::= term { ("+" | "-") term } ;

term            ::= factor { ("*" | "/") factor } ;

factor          ::= ("+" | "-" | "nao") factor
                  | integer
                  | string
                  | boolean
                  | identifier
                  | "rolar_dado" "(" integer ")" 
                  | "(" expression ")" ;

boolean         ::= "verdadeiro" | "falso" ;

identifier      ::= letter { letter | digit | "_" } ;

integer         ::= digit { digit } ;

string          ::= '"' { any_character_except_quote } '"' ;

NEWLINE         ::= '\n' ;